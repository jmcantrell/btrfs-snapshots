#compdef btrfs-snapshots

local curcontext=$curcontext context state state_descr line opt_args

_arguments -C -s -S -A "-*" : \
    "(- : *)"{-h,--help}"[show the help text]" \
    "(- : *)"{-v,--version}"[show the version number]" \
    ':action:((
        "profiles\:print profile names"
        "list\:print snapshot paths"
        "create\:create a new snapshot"
        "prune\:delete old snapshots"
    ))' \
    "*:: :->profile" \
    && return

service=$line[1]
curcontext=${curcontext%:*}-$service

case $state in
profile)
    case $service in
        list|create|prune)
            local profile

            local -A used_profiles=()

            for profile in "${line[@]:1}"; do
                used_profiles[$profile]=1
            done

            local profiles=()

            while IFS= read -r profile; do
                if ((!$+used_profiles[$profile])); then
                    profiles+=("$profile")
                fi
            done < <(btrfs-snapshots profiles)

            local expl
            _wanted values expl "profile" compadd -a profiles && return
            ;;
    esac
    ;;
esac

return 1
